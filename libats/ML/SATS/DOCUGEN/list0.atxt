#decltitle("\
ATSLIB/libats/ML/list0\
")

(* ****** ****** *)

#declpreamble("\

The functions declared in this package are primarily for supporting
ML-style processing of list-values. The programmer is encouraged to use the
dependent datatype #stacode("list") instead of #stacode("list0") after
gaining familiarity with dependent types.

")

(* ****** ****** *)

#declname("list0")
#declsynopsis2("\

The full name of the list0-type constructor is
#stacode("list0_t0ype_type"), which is given to the datatype declared as
follows:

#pats2xhtmls('\
datatype // t@ype+: covariant
list0_t0ype_type (a: t@ype+) =
  | list0_cons of (a, list0_t0ype_type a)
  | list0_nil of ()
// end of [list0_t0ype_type]
')
")

(* ****** ****** *)

#declnamesynop("list0_of_list")
#decldescrpt("\
This function casts a list-value of indexed type to a list-value of
unindexed type.
")
#declnamesynop("list0_of_list_vt")
#decldescrpt("\
This function casts a linear list-value of indexed type to a list-value of
unindexed type.
")
#declnamesynop("list_of_list0")
#decldescrpt("\
This function casts a list-value of unindexed type to a list-value of
indexed type.
")

(* ****** ****** *)

#declnamesynop("list0_head_exn")
#decldescrpt("\
This function returns the head of a given list if the list
is non-empty or raises an exception (ListSubscriptExn) if
the list is empty.
")
#declnamesynop("list0_head_opt")
#decldescrpt("\
This function is the optional version of #dyncode("list0_head_exn").
")

(* ****** ****** *)

#declnamesynop("list0_tail_exn")
#decldescrpt("\
This function returns the tail of a given list if the list
is non-empty or raises an exception (ListSubscriptExn) if
the list is empty.
")
#declnamesynop("list0_tail_opt")
#decldescrpt("\
This function is the optional version of #dyncode("list0_tail_exn").
")

(* ****** ****** *)

#declnamesynop("list0_nth_exn")
#decldescrpt("\

This function returns element i of a given list if i is a natural
number less than the length of the list.  Otherwise, it raises an
exception (ListSubscriptExn).

")
#declnamesynop("list0_nth_opt")
#decldescrpt("\
This function is the optional version of #dyncode("list0_nth_exn").
")

(* ****** ****** *)

#declnamesynop("list0_length")

#declnamesynop("list0_append")
#decldescrpt("\
Given two lists xs and ys, this function returns the concatenation of xs and ys.
")
#declexample("\
#pats2xhtmld("
implement
main () = let
  \#define M 10
  \#define N 20
  val xs = list0_make_elt<char> (M, 'a')
  val ys = list0_make_elt<char> (N, 'b')
  val xsys = list0_append (xs, ys)
  val () = assert (list0_length (xs) + list0_length (ys) = list0_length (xsys))
in
  0(*normal*)
end // end of [main]
")\

")

(* ****** ****** *)

#declnamesynop("list0_reverse")
#declnamesynop("list0_reverse_append")
#declnamesynop("list0_concat")

(* ****** ****** *)

#declnamesynop("list0_app")
#decldescrpt("\
This function is idential to #dyncode("list0_foreach").
")

(* ****** ****** *)

#declnamesynop("list0_foreach")
#decldescrpt("\

This function applies its second argument to each element in its first
argument.  ")

#declnamesynop("list0_iforeach")
#decldescrpt("\

This function applies its second argument f to (i, x), where i ranges from
0 to the length of xs minus 1 and x is element i in xs.

")

#declnamesynop("list0_foreach2")
#decldescrpt("\

Given two lists xs and ys of length m and n, respectively, this function
applies its third argument to each pair (xs[i], ys[i]), where i ranges from
0 until min(m,n)-1, inclusively, and xs[i] (ys[i]) refers to element i in
the list xs (ys).  ")

(* ****** ****** *)

#declnamesynop("list0_foldleft")
#decldescrpt("\

Given a list xs of length n, this function returns the value f (...f (f
(ini, xs[0]), xs[1])..., xs[n-1]), where the notation xs[i] refers to
element i in the list xs. The implementation of this function is
tail-recursive.

")

#declnamesynop("list0_ifoldleft")
#decldescrpt("\

Given a list xs of length n, this function returns the value f (...f (f
(ini, 0, xs[0]), 1, xs[1])..., n-1, xs[n-1]), where the notation xs[i]
refers to element i in the list xs. The implementation of this function is
tail-recursive.

")

#declnamesynop("list0_foldleft2")
#decldescrpt("\

Given two lists xs1 and xs2 of length n1 and n2, respectively, this
function returns the value f (...f (ini, xs1[0], xs2[0])..., xs1[n-1],
xs2[n-1]), where n equals min(n1, n2).

")

(* ****** ****** *)

#declnamesynop("list0_foldright")
#decldescrpt("\

Given a list xs of length n, this function returns the value f (xs[0], f
(xs[1], ...f (xs[n-1], snk)...)), where the notation xs[i] refers to
element i in the list xs. The implementation of this function is #emph("not")
tail-recursive, and stack-overflow may occur if the xs is long (e.g.,
containing 1,000,000 elements).

")

(* ****** ****** *)

#declnamesynop("list0_exists")
#decldescrpt("\

This function returns true if and only if there exists an element in its
first argument that satisfies the predicated provided as its second
argument.  ")

#declnamesynop("list0_exists2")
#decldescrpt("\

Given two lists xs1 and xs2 of length n1 and n2, respectively, this
function returns true if and only if p(xs1[i], xs2[i]) returns true for
some i between 0 and min(n1,n2)-1, inclusively.  ")

(* ****** ****** *)

#declnamesynop("list0_forall")
#decldescrpt("\

This function returns true if and only if every element in its first
argument satisfies the predicated provided as its second argument.  ")

(* ****** ****** *)

#declnamesynop("list0_forall2")
#decldescrpt("\

Given two lists xs1 and xs2 of length n1 and n2, respectively, this
function returns true if and only if p(xs1[i], xs2[i]) returns true for
every i between 0 and min(n1,n2)-1, inclusively.  ")

#declnamesynop("list0_forall2_eq")
#decldescrpt("\

This function is mostly similar to #dyncode("list0_forall2").  Given two
lists xs1 and xs2 of length n1 and n2, respectively, this function returns
true if and only if p(xs1[i], xs2[i]) returns true for every i between 0
and min(n1,n2)-1, inclusively. If this function returns true, then the value
stored in the call-by-reference parameter eq equals sgn(n1-n2). Otherwise,
the value equals 0.  ")

#declexample("\

The following code implements a function that checks whether two given
lists are equal based on a given equality funtion on list elements:

#pats2xhtmld("\
fun{
a:t@ype
} listeq (
  xs1: list0 a
, xs2: list0 a
, feq: cfun (a, a, bool)
) : bool = let
  var eq: int // uninitialied
  val ans = list0_forall2_eq (xs1, xs2, feq, eq)
in
  if eq = 0 then
    ans // [xs1] and [xs2] are of the same length
  else
    false // [xs1] and [xs2] are of different length
  // end of [if]
end // end of [listeq]
")
")

(* ****** ****** *)

#declnamesynop("list0_find_exn")
#decldescrpt("\
This functions returns the first element in its first argument that
satisfies the predicate given as its second argument. In case that such
an element does not exist, the function raises an exception (NotFoundExn).
")
#declnamesynop("list0_find_opt")
#decldescrpt("\
This function is the optional version of #dyncode("list0_find_exn").
")

(* ****** ****** *)

#declnamesynop("list0_map")
#decldescrpt("\

This function returns a list consisting of the sequence of results obtained
from applying its second argument f to each element in its first argument xs.
Following is a standard implementation of this function:

#pats2xhtmld("
implement{a}{b}
list0_map (xs, f) = let
in
//
case+ xs of
| list0_cons (x, xs) =>
    list0_cons (f x, list0_map (xs, f))
| list0_nil () => list0_nil ()
//
end // end of [list0_map]
")

However, the implementation of this function in ATSLIB is tail-recursive.\

")

(* ****** ****** *)

#declnamesynop("list0_imap")
#decldescrpt("\

Given a list xs, this function returns a list consisting of the sequence of
results obtained from applying its second argument f to each pair (i,
xs[i]), where i ranges from 0 until the length of xs minus 1, inclusively,
and xs[i] refers to element i in the list xs.

Following is a standard implementation of this function:

#pats2xhtmld("
implement{a}{b}
list0_imap (xs, f) = let
//
fun aux (
  i: int, xs: list0 a, f: cfun2 (int, a, b)
) : list0 b = let
in
  case+ xs of
  | list0_cons (x, xs) =>
      list0_cons (f (i, x), aux (i+1, xs, f))
  | list0_nil () => list0_nil ()
end // end of [aux]
//
in
  aux (0, xs, f)
end // end of [list0_imap]
")\

However, the implementation of this function in ATSLIB is tail-recursive.\

")

(* ****** ****** *)

#declnamesynop("list0_map2")
#decldescrpt("\

This function returns a list consisting of the sequence of results obtained
from applying its third argument f to each pair in the zip of the first and
second arguments.

Following is a standard implementation of this function:

#pats2xhtmld("
implement{a1,a2}{b}
list0_map2 (xs1, xs2, f) = let
in
//
case+ xs1 of
| list0_cons (x1, xs1) => (
  case+ xs2 of
  | list0_cons (x2, xs2) =>
      list0_cons (f (x1, x2), list0_map2 (xs1, xs2, f))
  | list0_nil () => list0_nil ()
  )
| list0_nil () => list0_nil ()
//
end // end of [list0_map2]
")

However, the implementation of this function in ATSLIB is tail-recursive.\

")

(* ****** ****** *)

#declnamesynop("list0_filter")
#decldescrpt("\

This function returns a list consisting of the sequence of elements in its
first argument that satisfy the predicate provided as its second argument.

Following is a standard implementation of this function:

#pats2xhtmld("
implement{a}
list0_filter (xs, p) = let
in
//
case+ xs of
| list0_cons (x, xs) =>
    if p (x) then
      list0_cons (x, list0_filter<a> (xs, p))
    else
      list0_filter<a> (xs, p)
    // end of [if]
| list0_nil () => list0_nil ()
//
end // end of [list0_filter]
")

However, the implementation of this function in ATSLIB is tail-recursive.\

")

(* ****** ****** *)

#declnamesynop("list0_zip")
#decldescrpt("\

Given two lists xs and ys of length m and n, respectively, this function
returns a list of pairs (xs[i], ys[i]), where i ranges from 0 until
min(m,n)-1 and xs[i] (ys[i]) refers to element i in xs (ys).\

#pats2xhtmld("
implement
{x,y}
list0_zip (xs, ys) = let
in
//
case+ xs of
| list0_cons (x, xs) => (
  case+ ys of
  | list0_cons (y, ys) =>
      list0_cons (@(x, y), list0_zip<x,y> (xs, ys))
  | list0_nil () => list0_nil ()
  )
| list0_nil () => list0_nil ()
//
end // end of [list0_zip]
")

However, the implementation of this function in ATSLIB is tail-recursive.\

")

(* ****** ****** *)

#declnamesynop("list0_zipwith")
#decldescrpt("\
This function does exactly the same as #dyncode("list0_map2").
")

(* ****** ****** *)

(* end of [list0.atxt] *)
