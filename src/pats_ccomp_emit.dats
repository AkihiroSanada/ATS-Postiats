(***********************************************************************)
(*                                                                     *)
(*                         Applied Type System                         *)
(*                                                                     *)
(***********************************************************************)

(*
** ATS/Postiats - Unleashing the Potential of Types!
** Copyright (C) 2011-20?? Hongwei Xi, ATS Trustful Software, Inc.
** All rights reserved
**
** ATS is free software;  you can  redistribute it and/or modify it under
** the terms of  the GNU GENERAL PUBLIC LICENSE (GPL) as published by the
** Free Software Foundation; either version 3, or (at  your  option)  any
** later version.
** 
** ATS is distributed in the hope that it will be useful, but WITHOUT ANY
** WARRANTY; without  even  the  implied  warranty  of MERCHANTABILITY or
** FITNESS FOR A PARTICULAR PURPOSE.  See the  GNU General Public License
** for more details.
** 
** You  should  have  received  a  copy of the GNU General Public License
** along  with  ATS;  see the  file COPYING.  If not, please write to the
** Free Software Foundation,  51 Franklin Street, Fifth Floor, Boston, MA
** 02110-1301, USA.
*)

(* ****** ****** *)
//
// Author: Hongwei Xi (gmhwxi AT gmail DOT com)
// Start Time: November, 2012
//
(* ****** ****** *)

staload "pats_errmsg.sats"
staload _(*anon*) = "pats_errmsg.dats"
implement prerr_FILENAME<> () = prerr "pats_trans3_dynexp_up"

(* ****** ****** *)

staload LAB = "pats_label.sats"
staload STMP = "pats_stamp.sats"

(* ****** ****** *)

staload FIL = "pats_filename.sats"

(* ****** ****** *)

staload SYM = "pats_symbol.sats"
staload SYN = "pats_syntax.sats"

(* ****** ****** *)

staload GLOB = "pats_global.sats"

(* ****** ****** *)

staload D2E = "pats_dynexp2.sats"

(* ****** ****** *)

staload HSE = "pats_histaexp.sats"

(* ****** ****** *)

staload "pats_ccomp.sats"

(* ****** ****** *)

local

staload
TM = "libc/SATS/time.sats"
stadef time_t = $TM.time_t

in // in of [local]

implement
emit_time_stamp (out) = let
//
var tm: time_t
val () = tm := $TM.time_get ()
val (pfopt | p_tm) = $TM.localtime (tm)
//
val () = fprint_string (out, "/*\n");
val () = fprint_string (out, "**\n");
val () = fprint_string (out, "** The C code is generated by ATS/Postiats\n");
val () = fprint_string (out, "** The compilation time is: ")
//
val () =
  if p_tm > null then let
  prval Some_v @(pf1, fpf1) = pfopt
  val tm_min = $TM.tm_get_min (!p_tm)
  val tm_hour = $TM.tm_get_hour (!p_tm)
  val tm_mday = $TM.tm_get_mday (!p_tm)
  val tm_mon = 1 + $TM.tm_get_mon (!p_tm)
  val tm_year = 1900 + $TM.tm_get_year (!p_tm)
  prval () = fpf1 (pf1)
in
  fprintf (out
  , "%i-%i-%i: %2ih:%2im\n"
  , @(tm_year, tm_mon, tm_mday, tm_hour, tm_min)
  )
end else let
  prval None_v () = pfopt
in
  fprintf (out, "**UNKNOWN**\n", @())
end // end of [if]
//
val () = fprint_string (out, "**\n")
val () = fprint_string (out, "*/\n\n")
//
in
// nothing
end // end of [emit_time_stamp]

end // end of [local]

(* ****** ****** *)

implement
emit_label
  (out, lab) = $LAB.fprint_label (out, lab)
// end of [emit_label]

(* ****** ****** *)

implement
emit_ident
  (out, name) = fprint_string (out, name)
// end of [emit_ident]

implement
emit_filename
  (out, fil) = let
  val sym =
    $FIL.filename_get_full (fil)
  // end of [val]
  val name = $SYM.symbol_get_name (sym)
in
  emit_ident (out, name)
end // end of [emit_filename]

(* ****** ****** *)

implement
emit_d2cst
  (out, d2c) = let
  val extdef =
    $D2E.d2cst_get_extdef (d2c)
  // end of [val]
in
  case+ extdef of
  | $SYN.DCSTEXTDEFnone () => let
      val fil = $D2E.d2cst_get_fil (d2c)
      val sym = $D2E.d2cst_get_sym (d2c)
      val name = $SYM.symbol_get_name (sym)
      val () = emit_filename (out, fil)
      val () = fprint_string (out, "__")
      val () = emit_ident (out, name)
    in
      // nothing
    end // end of [DCSTEXTDEFnone]
  | $SYN.DCSTEXTDEFsome_ext name => emit_ident (out, name)
  | $SYN.DCSTEXTDEFsome_sta name => emit_ident (out, name)
  | $SYN.DCSTEXTDEFsome_mac name => emit_ident (out, name)
end // end of [emit_d2cst]

(* ****** ****** *)

implement
emit_funlab
  (out, fl) = let
//
val opt = funlab_get_qopt (fl)
//
val () = (
  case+ opt of
  | Some (d2c) => let
      val () = emit_d2cst (out, d2c)
    in
    end // end of [Some]
  | None () => let
      val () = emit_ident (out, funlab_get_name (fl))
    in
    end // end of [None]
) : void // end of [val]
in
  // nothing
end // end of [emit_funlab]

(* ****** ****** *)

implement
emit_tmpvar
  (out, tmp) = let
//
val knd = tmpvar_get_tpknd (tmp)
//
val () = (case+ 0 of
  | _ when knd = 0 => let
    in
      fprint_string (out, "tmp") // local temporary variable
    end // end of [_]
  | _ (*(static)top*) => let
    in
      fprint_string (out, "statmp") // static toplevel temporary
    end // end of [knd = 1]
) : void // end of [val]
//
val stmp = tmpvar_get_stamp (tmp)
//
in
  $STMP.fprint_stamp (out, stmp)
end // end of [emit_tmpvar]

(* ****** ****** *)

implement
emit_tmpvar_assgn
  (out, tmp) = {
  val () = emit_tmpvar (out, tmp)
  val () = fprint_string (out, " = ")
} // end of [emit_tmpvar_assgn]

(* ****** ****** *)

typedef
emit_primval_type = (FILEref, primval) -> void

(* ****** ****** *)

extern fun emit_primval_tmp : emit_primval_type
extern fun emit_primval_tmpref : emit_primval_type
extern fun emit_primval_arg : emit_primval_type
extern fun emit_primval_argref : emit_primval_type
extern fun emit_primval_bool : emit_primval_type

(* ****** ****** *)

implement
emit_primval
  (out, pmv0) = let
  val loc0 = pmv0.primval_loc
in
//
case+ pmv0.primval_node of
| PMVtmp _ => emit_primval_tmp (out, pmv0)
| PMVtmpref _ => emit_primval_tmpref (out, pmv0)
| PMVarg _ => emit_primval_arg (out, pmv0)
(*
| PMVargref _ => emit_primval_argref (out, pmv0)
*)
| PMVbool _ => emit_primval_bool (out, pmv0)
| _ => let
    val () = prerr_interror_loc (loc0)
    val () = (
      prerr ": emit_primval: pmv0 = "; prerr pmv0; prerr_newline ()
    ) // end of [val]
    val () = assertloc (false)
  in
    // nothing
  end // end of [_]
//
end // end of [emit_primval]

(* ****** ****** *)

implement
emit_primvalist
  (out, pmvs) = let
//
fun loop (
  out: FILEref, pmvs: primvalist, i: int
) : void = let
in
//
case+ pmvs of
| list_cons
    (pmv, pmvs) => let
    val () =
      if i > 0 then fprint_string (out, ", ")
    // end of [val]
    val () = emit_primval (out, pmv)
  in
    loop (out, pmvs, i+1)
  end // end of [list_cons]
| list_nil () => ()
//
end // end of [loop]
//
in
  loop (out, pmvs, 0)
end // end of [emit_primvalist]

(* ****** ****** *)

implement
emit_primval_tmp
  (out, pmv0) = let
//
val- PMVtmp (tmp) = pmv0.primval_node
//
in
  emit_tmpvar (out, tmp)
end // end of [emit_primval_tmp]

implement
emit_primval_tmpref
  (out, pmv0) = let
//
val- PMVtmpref (tmp) = pmv0.primval_node
//
in
  emit_tmpvar (out, tmp)
end // end of [emit_primval_tmpref]

(* ****** ****** *)

implement
emit_primval_arg
  (out, pmv0) = let
//
val- PMVarg (ind) = pmv0.primval_node
//
in
  fprintf (out, "arg%i", @(ind))
end // end of [emit_primval_arg]

(* ****** ****** *)

implement
emit_primval_bool
  (out, pmv0) = let
//
val- PMVbool (b) = pmv0.primval_node
val name = (
  if b then "atsbool_true" else "atsbool_false"
) : string // end of [val]
//
in
  fprint_string (out, name)
end (* end of [emit_primval_bool] *)

(* ****** ****** *)

extern
fun emit_instr_funcall (out: FILEref, ins: instr): void

(* ****** ****** *)

implement
emit_instr
  (out, ins) = let
//
val loc0 = ins.instr_loc
//
// generating #line progma for debugging
//
val gline =
  $GLOB.the_DEBUGATS_dbgline_get ()
val () = (
  if gline > 0 then $LOC.fprint_line_pragma (out, loc0)
) : void // end of [val]
//
val gflag =
  $GLOB.the_DEBUGATS_dbgflag_get ()
val () = (
//
// HX: generating debug information
//
  if gflag > 0 then let
    val () = fprint_string (out, "/* ")
    val () = fprint_instr (out, ins)
    val () = fprint_string (out, " */\n")
  in
    // empty
  end // end of [if]
) : void // end of [val]
//
in
//
case+ ins.instr_node of
| INSfuncall _ => emit_instr_funcall (out, ins)
| _ => let
    val () = prerr_interror_loc (loc0)
    val () = (
      prerr ": emit_instr: ins = "; prerr_instr (ins); prerr_newline ()
    ) // end of [val]
    val () = assertloc (false)
  in
    // nothing
  end // end of [_]
end // end of [emit_instr]

(* ****** ****** *)

implement
emit_instrlst
  (out, inss) = let
//
fun loop (
  out: FILEref, inss: instrlst, sep: string, i: int
) : void = let
in
//
case+ inss of
| list_cons
    (ins, inss) => let
    val () =
      if i > 0 then
        fprint_string (out, sep)
      // end of [if]
    val () = emit_instr (out, ins)
  in
    loop (out, inss, sep, i+1)
  end // end of [list_cons]
| list_nil () => let
    val () =
      if i = 0 then fprint_string (out, "/* (*nothing*) */")
    // end of [val]
  in
    // nothing
  end // end of [list_nil]
//
end // end of [loop]
//
in
  loop (out, inss, "\n", 0)
end // end of [emit_instrlst]    

(* ****** ****** *)

implement
emit_instr_funcall
  (out, ins) = let
//
val loc0 = ins.instr_loc
val- INSfuncall
  (tmp, pmv_fun, hse_fun, pmvs_arg) = ins.instr_node
//
val () = (
  println! ("emit_instr_funcall: pmv_fun = ", pmv_fun)
) // end of [val]
//
val isvoid = false
val () = if isvoid then fprint_string (out, "/* ")
val () = emit_tmpvar_assgn (out, tmp)
val () = if isvoid then fprint_string (out, "*/ ")
//
in
//
case+ pmv_fun.primval_node of
//
| PMVfun (fl) => let
    val () = emit_funlab (out, fl)
    val () = fprint_string (out, "(")
    val () = emit_primvalist (out, pmvs_arg)
    val () = fprint_string (out, ") ;")
  in
    // nothing
  end // end of [VPfun]
//
| _ => let
    val () = prerr_interror_loc (loc0)
    val () = (
      prerr ": emit_instr_funcall: hse_fun = "; $HSE.prerr_hisexp (hse_fun); prerr_newline ()
    ) // end of [val]
    val () = assertloc (false)
  in
    // nothing
  end // end of [_]
//
end // end of [emit_instr_funcall]

(* ****** ****** *)

(* end of [pats_ccomp_emit.dats] *)
