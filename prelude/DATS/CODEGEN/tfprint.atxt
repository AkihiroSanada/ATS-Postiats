%{
#define ATSCODEFORMAT "txt"
#if (ATSCODEFORMAT == "txt")
#include "utils/atsdoc/HATS/postiatsatxt.hats"
#endif // end of [ATSCCODEFORMAT]
val _thisfilename = atext_strcst"tfprint.dats"
val () = theAtextMap_insert_str ("thisfilename", _thisfilename)
%}\
\
#atscode_banner()
#atscode_copyright_LGPL()

#atscode_separator()

(*
** Source:
** $PATSHOME/prelude/DATS/CODEGEN/tfprint.atxt
** Time of generation: #timestamp()
*)

#atscode_separator()

#atscode_author("Hongwei Xi")
#atscode_authoremail("hwxi AT cs DOT bu DOT edu")
#atscode_start_time("August, 2012")

#atscode_separator()

implement{a}
tfprint (x) = let
  val out = tfprint$out () in fprint_val<a> (out, x)
end // end of [tfprint]

implement{} tfprint$out () = stdout_ref

#atscode_separator()

implement{}
tfprint_list$sep () = ","
implement{}
tfprint_list$beg () = "("
implement{}
tfprint_list$end () = ")"

implement{a}
tfprint_list (xs) = let
//
val out = tfprint$out ()
val sep = tfprint_list$sep ()
val _beg = tfprint_list$beg ()
and _end = tfprint_list$end ()
//
val () = fprint_string (out, _beg)
val () = fprint_list_sep (out, xs, sep)
val () = fprint_string (out, _end)
//
in
end // end of [tfprint_list]

implement(a) tfprint<List(a)> (xs) = tfprint_list<a> (xs)

#atscode_separator()

implement{}
tfprint_array$sep () = ","
implement{}
tfprint_array$beg () = "["
implement{}
tfprint_array$end () = "]"

implement{a}
tfprint_array (A, n) = let
//
val out = tfprint$out ()
val sep = tfprint_array$sep ()
val _beg = tfprint_list$beg ()
and _end = tfprint_list$end ()
//
val () = fprint_string (out, _beg)
val () = fprint_array_sep (out, A, n, sep)
val () = fprint_string (out, _end)
//
in
  // nothing
end // end of [tfprint_array]

implement{a}
tfprint_arrayptr
  (A, n) = () where {
  val p = ptrcast (A)
  prval pf = arrayptr_takeout (A)
  val () = tfprint_array<a> (!p, n)
  prval () = arrayptr_addback (pf | A)
} // end of [tfprint_arrayptr]

#atscode_separator()

implement{a}
tfprint_arrayref
  (A, n) = () where {
  val (vbox pf | p) = arrayref_get_viewptr (A)
  val () = $effmask_ref (tfprint_array<a> (!p, n))
} // end of [tfprint_arrayref]

#atscode_separator()

#atscode_eof_strsub("\#thisfilename$")\

%{
implement main () = fprint_filsub (stdout_ref, "tfprint_atxt.txt")
%}\
