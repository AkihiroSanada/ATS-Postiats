%{
#define ATSCODEFORMAT "txt"
#if (ATSCODEFORMAT == "txt")
#include "utils/atsdoc/HATS/postiatsatxt.hats"
#endif // end of [ATSCCODEFORMAT]
val _thisfilename = atext_strcst"list_vt.dats"
val () = theTextMap_insert_str ("thisfilename", _thisfilename)
%}\
\
#atscode_banner()
#atscode_copyright_LGPL()

#atscode_separator()

(*
** Source:
** $PATSHOME/prelude/DATS/CODEGEN/list_vt.atxt
** Time of generation: #timestamp()
*)

#atscode_separator()

#atscode_author("Hongwei Xi")
#atscode_authoremail("hwxi AT cs DOT bu DOT edu")
#atscode_start_time("Feburary, 2012")

#atscode_separator()

implement{a}
compare_elt_elt
  (x1, x2, cmp) = cmp (x1, x2)
// end of [compare_elt_elt]

implement{a}
compare_elt_elt_env
  (x1, x2, cmp, env) = cmp (x1, x2, env)
// end of [compare_elt_elt_env]

#atscode_separator()

implement{}
list_vt_is_nil (xs) =
  case+ xs of list_vt_nil () => true | _ =>> false
implement{}
list_vt_is_cons (xs) =
  case+ xs of list_vt_cons _ => true | _ =>> false

#atscode_separator()

implement{a}
list_vt_length (xs) = let
  prval () = lemma_list_vt_param (xs)
  fun loop
    {i,j:nat} .<i>. (
    xs: !list_vt (a, i), j: int j
  ) : int (i+j) =
    case+ xs of
    | list_vt_cons (_, xs) => loop (xs, j + 1)
    | list_vt_nil () => j
  // end of [loop]
in
  loop (xs, 0)
end // end of [list_vt_length]

#atscode_separator()

implement{a}
list_vt_reverse (xs) =
  list_vt_reverse_append (xs, list_vt_nil)
// end of [list_vt_reverse]

#atscode_separator()

implement{a}
list_vt_reverse_append
  (xs, ys) = let
//
fun loop
  {m,n:nat} .<m>. (
  xs: list_vt (a, m), ys: list_vt (a, n)
) :<> list_vt (a, m+n) =
  case xs of
  | @list_vt_cons
      (_, xs1) => let
      val xs1_v = xs1
      val () = xs1 := ys
      prval () = fold@ (xs)
    in
      loop (xs1_v, xs)
    end
  | ~list_vt_nil () => ys
// end of [loop]
in
  loop (xs, ys)
end // end of [list_vt_reverse_append]

#atscode_separator()

implement{a}
list_vt_foreach_funenv
  {v}{vt}{n}{fe}
  (pf | xs, f, env) = let
  fun loop
    {n:nat} .<n>. (
    pf: !v
  | xs: !list_vt (a, n)
  , f: (!v | &a, !vt) -<fe> void
  , env: !vt
  ) :<fe> void =
    case+ xs of
    | @list_vt_cons
        (x, xs1) => let
        val () = f (pf | x, env)
        val () = loop (pf | xs1, f, env)
      in
        fold@ (xs)
      end // end of [list_vt_cons]
    | list_vt_nil () => ()
  // end of [loop]
in
  loop (pf | xs, f, env)
end // end of [list_vt_foreach_funenv]

#atscode_separator()

implement{a}
list_vt_make_array
  {n} (A, n) = let
  prval () =
    lemma_array_v_param (view@(A))
  viewtypedef res(n:int) = list_vt (a, n)
  fun loop
    {l:addr}
    {n:nat} .<n>. (
    pf: !array_v (a, l, n) >> array_v (a?!, l, n)
  | p: ptr l
  , n: size_t n
  , res: &ptr? >> res(n)
  ) :<> void =
    if n > 0 then let
      prval (
        pf1, pf2
      ) = array_v_uncons (pf)
      val () = res :=
        list_vt_cons {a}{0} (_, _)
      val+ list_vt_cons (x, res1) = res
      val () = x := !p
      val () = loop (pf2 | p+sizeof<a>, pred(n), res1)
      prval () = pf := array_v_cons (pf1, pf2)
      prval () = fold@ (res)
    in
      // nothing
    end else let
      prval () = array_v_unnil (pf)
      prval () = pf := array_v_nil ()
    in
      res := list_vt_nil ()
    end // end of [if]
  // end of [loop]
  var res: ptr // uninitialized
  val () = loop (view@(A) | addr@(A), n, res)
in
  res
end // end of [list_vt_make_array]

#atscode_separator()

implement{a}
list_vt_mergesort
  {n} (xs, cmp) = let
  var env: ptr = null
  val cmp = __cast (cmp) where {
    extern castfn __cast (cmp: cmp (a)): cmp (a, ptr)
  } // end of [val]
in
  list_vt_mergesort_env (xs, cmp, env)
end // end of [list_vt_mergesort]

implement{a}
list_vt_mergesort_env
  {vt}{n} (xs, cmp, env) = let
//
fun split {n,n1:nat | n >= n1} .<n1>. (
    xs: &list_vt (a, n) >> list_vt (a, n1)
  , n1: int n1, res: &List_vt a? >> list_vt (a, n-n1)
  ) :<> void =
  if n1 > 0 then let
    val+ @list_vt_cons (_, xs1) = xs
    val () = split (xs1, n1-1, res)
  in
    fold@ (xs)
  end else let
    val () = res := xs
    val () = xs := list_vt_nil ()
  in
    // nothing
  end // end of [if]
// end of [split]
fun merge {n1,n2:nat} .<n1+n2>. (
    xs1: list_vt (a, n1)
  , xs2: list_vt (a, n2)
  , cmp: cmp (a, vt), env: !vt
  , res: &List_vt a? >> list_vt (a, n1+n2)
  ) :<> void =
  case+ xs1 of
  | @list_vt_cons (x1, xs11) => (
    case+ xs2 of
    | @list_vt_cons (x2, xs21) => let
        val sgn =
          compare_elt_elt_env (x1, x2, cmp, env)
        // end of [val]
      in
        if sgn <= 0 then let
          prval () = fold@ {a} (xs2)
          val () = merge (xs11, xs2, cmp, env, xs11)
          prval () = fold@ {a} (xs1)
        in
          res := xs1
        end else let
          prval () = fold@ {a} (xs1)
          val () = merge (xs1, xs21, cmp, env, xs21)
          prval () = fold@ {a} (xs2)
        in
          res := xs2
        end // end of [if]
      end // end of [list_vt_cons]
    | ~list_vt_nil () => (fold@ (xs1); res := xs1)
    ) // end of [list_vt_cons]
  | ~list_vt_nil () => (res := xs2)
// end of [merge]
//
val n = list_vt_length<a> (xs)
//
in
//
if n >= 2 then let
  val+ @list_vt_cons (_, xs1) = xs
  var res: List_vt a? // uninitialized
  val () = split (xs1, (n-1)/2, res)
  prval () = fold@ (xs)
  val xs1 = list_vt_mergesort_env<a> (xs, cmp, env)
  val xs2 = list_vt_mergesort_env<a> (res, cmp, env)
  val () = merge (xs1, xs2, cmp, env, res)
in
  res
end else xs // end of [if]
//
end // end of [list_vt_mergesort_env]

#atscode_separator()

implement{a}
list_vt_quicksort
  {n} (xs, cmp) = let
  var env: ptr = null
  val cmp = __cast (cmp) where {
    extern castfn __cast (cmp: cmp (a)): cmp (a, ptr)
  } // end of [val]
in
  list_vt_quicksort_env (xs, cmp, env)
end // end of [list_vt_quicksort]

#atscode_separator()

#atscode_eof_strsub("\#thisfilename$")\

%{
implement main () = fprint_filsub (stdout_ref, "list_vt_atxt.txt")
%}\
