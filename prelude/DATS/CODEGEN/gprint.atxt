%{
#define ATSCODEFORMAT "txt"
#if (ATSCODEFORMAT == "txt")
#include "utils/atsdoc/HATS/postiatsatxt.hats"
#endif // end of [ATSCCODEFORMAT]
val _thisfilename = atext_strcst"gprint.dats"
val () = theAtextMap_insert_str ("thisfilename", _thisfilename)
%}\
\
#atscode_banner()
#atscode_copyright_LGPL()

#atscode_separator()

(*
** Source:
** $PATSHOME/prelude/DATS/CODEGEN/gprint.atxt
** Time of generation: #timestamp()
*)

#atscode_separator()

#atscode_author("Hongwei Xi")
#atscode_authoremail("hwxi AT cs DOT bu DOT edu")
#atscode_start_time("August, 2012")

#atscode_separator()

implement{} gprint$out () = stdout_ref

#atscode_separator()

implement{a}
gprint (x) = let
  val out = gprint$out () in fprint_val<a> (out, x)
end // end of [gprint]

#atscode_separator()

implement{}
gprint_newline () = let
  val out = gprint$out () in fprint_newline (out)
end // end of [gprint_newline]

#atscode_separator()
//
implement{}
gprint_int (x) =
  fprint_val<int> (gprint$out (), x)
implement{}
gprint_char (x) =
  fprint_val<char> (gprint$out (), x)
implement{}
gprint_float (x) =
  fprint_val<float> (gprint$out (), x)
implement{}
gprint_double (x) =
  fprint_val<double> (gprint$out (), x)
implement{}
gprint_string (x) =
  fprint_val<string> (gprint$out (), x)
//
implement gprint<int> (x) = gprint_int (x)
implement gprint<char> (x) = gprint_char (x)
implement gprint<float> (x) = gprint_float (x)
implement gprint<double> (x) = gprint_double (x)
implement gprint<string> (x) = gprint_string (x)
//
#atscode_separator()

implement{}
gprint_list$beg () = "("
implement{}
gprint_list$end () = ")"
implement{}
gprint_list$sep () = ", "

#atscode_separator()

implement{a}
gprint_list (xs) = let
//
val out = gprint$out ()
//
val _beg = gprint_list$beg ()
and _end = gprint_list$end ()
val _sep = gprint_list$sep ()
//
val () = gprint_string (_beg)
val () = fprint_list_sep (out, xs, _sep)
val () = gprint_string (_end)
//
in
  // nothing
end // end of [gprint_list]

implement(a)
gprint<List(a)> (xs) = gprint_list<a> (xs)

#atscode_separator()

implement{}
gprint_listlist$beg1 () = "("
implement{}
gprint_listlist$end1 () = ")"
implement{}
gprint_listlist$sep1 () = ", "

implement{}
gprint_listlist$beg2 () = "("
implement{}
gprint_listlist$end2 () = ")"
implement{}
gprint_listlist$sep2 () = ", "

implement{a}
gprint_listlist
  (xss) = let
//
typedef xs = List (a)
//
implement
fprint_val<xs>
  (out, xs) = let
//
implement gprint$out<> () = out
implement gprint_list$beg<> () = gprint_listlist$beg2 ()
implement gprint_list$end<> () = gprint_listlist$end2 ()
implement gprint_list$sep<> () = gprint_listlist$sep2 ()
//
in
  gprint_list<a> (xs)
end // end of [fprint_val]
//
implement gprint_list$beg<> () = gprint_listlist$beg1 ()
implement gprint_list$end<> () = gprint_listlist$end1 ()
implement gprint_list$sep<> () = gprint_listlist$sep1 ()
//
in
  gprint_list<xs> (xss)
end // end of [gprint_listlist]

#atscode_separator()

implement{}
gprint_array$beg () = "@("
implement{}
gprint_array$end () = ")"
implement{}
gprint_array$sep () = ", "

#atscode_separator()

implement{a}
gprint_array (A, n) = let
//
val out = gprint$out ()
//
val _beg = gprint_array$beg ()
and _end = gprint_array$end ()
val _sep = gprint_array$sep ()
//
val () = gprint_string (_beg)
val () = fprint_array_sep (out, A, n, _sep)
val () = gprint_string (_end)
//
in
  // nothing
end // end of [gprint_array]

#atscode_separator()

implement{a}
gprint_arrayptr
  (A, n) = () where {
  val p = ptrcast (A)
  prval pf = arrayptr_takeout (A)
  val () = gprint_array<a> (!p, n)
  prval () = arrayptr_addback (pf | A)
} // end of [gprint_arrayptr]

#atscode_separator()

implement{a}
gprint_arrayref
  (A, n) = () where {
  val (vbox pf | p) = arrayref_get_viewptr (A)
  val () = $effmask_ref (gprint_array<a> (!p, n))
} // end of [gprint_arrayref]

#atscode_separator()

#atscode_eof_strsub("\#thisfilename$")\

%{
implement main () = fprint_filsub (stdout_ref, "gprint_atxt.txt")
%}\
