#decltitle("\
ATSLIB/prelude/reference\
")

(* ****** ****** *)

#declpreamble('\
#para("\
A reference is just a persistent array of size 1. It is persistent in the
sense that the memory associated with a reference is heap-allocated and
can only be reclaimed by GC. While it is allowed to store inear values
in references, doing so demands caution as resources contained in such
values may be inadvertently leaked.
")
')#comment("declpreamble")

(* ****** ****** *)

#declnamesynop("ref_get_ptr")
#decldescript('\
This cast function (castfn) returns the pointer associated with a given
reference. Note that the returned pointer is guranteed to be non-null.
')

(* ****** ****** *)

#declnamesynop("ref_get_viewptr")
#decldescript('\
This cast function (castfn) returns the boxed at-view and pointer
associated with a given reference. If the value stored in a reference is
linear, then it is not allowed for the value be taken out from the
reference. Instead, #dyncode("ref_get_viewptr") can be called on the
reference to allow that the value be processed in situ.
')
#declexample('\
The following function #dyncode("ref_exch_at")
exchanges the contents of a reference and a variable:
#pats2xhtmld('\
fun{a:vt0p}
ref_exch_at (
  r: ref (a), x: &a >> a
) : void = let
  val (
    vbox pf | p
  ) = ref_get_viewptr (r)
  val tmp = !p
in
  !p := x; x := tmp
end // end of [ref_exch_at]
')
Note that the type for the contents of r and x can be linear.
')

(* ****** ****** *)

#declnamesynop("ref")
#decldescript('\
This function creates a reference initialized with its given argument.
')
#declnamesynop("ref_make_elt")
#decldescript('\
This function does the same as #dyncode("ref").
')

(* ****** ****** *)

#declnamesynop("ref_make_viewptr")
#decldescript('\
This cast function (castfn) turns a pointer with a proof of at-view into a
reference.
')
#declexample('\

The following code implements a counter:

#pats2xhtmld('\
local

var __count: int = 0 // it is statically allocated
val theCount =
  ref_make_viewptr {int} (view@(__count) | addr@(__count))
// end of [val]

in // in of [local]

fun theCount_get (): int = !theCount

fun theCount_inc (): void = !theCount := !theCount + 1

fun theCount_getinc
  (): int = let
  val n = !theCount; val () = !theCount := n + 1 in n
end // end of [theCount_getind]

fun theCount_reset (): void = !theCount := 0

end // end of [local]
')

The specialty of this implementation is that it does not make use of any
dynamic memory allocation. This is a programming style often necessary for
implementing low-level embedded systems.

')

(* ****** ****** *)

#declnamesynop("ref_get_elt")
#decldescript('\
This function returns the value stored in a given reference.
Note that the type of the value is nonlinear.
')

(* ****** ****** *)

#declnamesynop("ref_set_elt")
#decldescript('\

This function stores a value (the second argument) into a given reference
(the first argument).  Note that the type of the value is nonlinear.

')

(* ****** ****** *)

#declnamesynop("ref_exch_elt")
#decldescript('\

This function exchanges the value in a given reference (the first argument)
with the value in a variable ( the second argument).  Note that the type of
these values can be linear.  ')

(* ****** ****** *)

(* end of [reference.atxt] *)
