(* ****** ****** *)

%{
val () = patscode_count_reset()
val () = patscode_prefix_set("TRYIT/prelude_arith_prf")
%}

(* ****** ****** *)

%{

#ifdef DBOOK #then
fun pcenter (x: string): atext =
  atext_apptxt2 (atext_strsub (x), atext_strcst ".")
// end of [pcenter]
#endif // end of [#ifdef]

%}

(* ****** ****** *)

#decltitle("\
ATSLIB/prelude/arith_prf
")

(* ****** ****** *)

#declpreamble('\

#para("\
This package contains some common proof functions for handling
arithmetics.
")

')#comment("declpreamble")

(* ****** ****** *)

#declname("MUL")
#declsynop2("
#para("\
The dataprop #stacode("MUL") is declared as follows:
#pats2xhtmls("\
dataprop
MUL (int, int, int) =
  | {n:int}
    MULbas (0, n, 0)
  | {m:nat}{n:int}{p:int}
    MULind (m+1, n, p+n) of MUL (m, n, p)
  | {m:pos}{n:int}{p:int}
    MULneg (~m, n, ~p) of MUL (m, n, p)
// end of [MUL]
")")
")
#decldescrpt("\
Given integers m, n and p, the type (or more precisely, prop)
#stacode("MUL(m, n, p)") captures the relation #stacode("m*n=p").
")

(* ****** ****** *)

#declnamesynop("mul_make")
#decldescrpt('\

This proof function applies to static integers m and n to generate a proof
of the type #stacode("MUL(m, n, m*n)").

')

(* ****** ****** *)

#declnamesynop("mul_elim")
#decldescrpt('\

This proof function relates the relation #stacode("MUL") to the pre-defined
static multiplication function on integers.

')

(* ****** ****** *)

#declnamesynop("mul_istot")
#decldescrpt('\

This proof function applies to static integers m and n to generate a proof
of the type #stacode("MUL(m, n, p)") for some integer p. In other words,
the function proves that #stacode("MUL") is total on its first two
arguments. The following code gives a direct implementation of
#prfcode("mul_istot"):

#pats2xhtmld_tryit('\
primplmnt
mul_istot
  {m,n} () = let
//
prfun lemma
  {m:nat;n:int} .<m>. (): [p:int] MUL (m, n, p) =
  sif m > 0 then MULind (lemma {m-1,n} ()) else MULbas ()
//
in
  sif m >= 0 then lemma {m,n} () else MULneg (lemma {~m,n} ())
end // end of [mul_istot]
')

Note that #prfcode("mul_make") entirely supersedes #prfcode("mul_istot").

')

(* ****** ****** *)

#declnamesynop("mul_isfun")
#decldescrpt('\

This proof function proves that the relation #stacode("MUL") is functional
on its first two arguments.  The following code gives a direct
implementation of #prfcode("mul_isfun"):

#pats2xhtmld_tryit('\
primplmnt
mul_isfun
  {m,n} (pf1, pf2) = let
//
prfun lemma
  {m:nat;n:int}
  {p1,p2:int} .<m>. (
  pf1: MUL (m, n, p1), pf2: MUL (m, n, p2)
) : [p1==p2] void =
  case+ (pf1, pf2) of
  | (MULind (pf1), MULind (pf2)) => lemma (pf1, pf2)
  | (MULbas (), MULbas ()) => ()
//
in
  sif m >= 0 then
    lemma (pf1, pf2)
  else let
    prval MULneg (pf1) = pf1 and MULneg (pf2) = pf2
  in
    lemma (pf1, pf2)
  end (* end of [sif] *)
end // end of [mul_isfun]
')

')

(* ****** ****** *)

#declnamesynop("mul_isfun2")
#decldescrpt('\
This proof function is a variation of #prfcode("mul_isfun").
')

(* ****** ****** *)

#declnamesynop("mul_add_const")
#decldescrpt('\

This proof function essentially captures the identity:
#stacode("(m+i)*n=m*n+i*n").  It is kept mostly for the sake of backward
compatibility as the constraint-solver for ATS2 (Postiats) is able to
verify such an identity automatically. For instance, this ability is made
of use in typechecking the following implementation of
#dyncode("mul_add_const"):

#pats2xhtmld_tryit('\
primplmnt
mul_add_const
  {i}{m,n}{p} (pf) = let
  prval () = mul_elim (pf) in mul_make {m+i,n} ()
end // end of [mul_add_const]
')
')

(* ****** ****** *)

#declnamesynop("mul_expand_linear")
#decldescrpt('\

This proof function essentially captures the following identity:
#pcenter("\
#stacode("(a*x+b)*(c*y+d) = a*c*xy+a*d*x+b*c*y+b*d")\
")

It is kept mostly for the sake of backward compatibility.  ')

(* ****** ****** *)

#declnamesynop("mul_expand2_linear")
#decldescrpt('\

This proof function essentially captures the following identity:
#pcenter("\
#stacode("\
(a1*x1+a2*x2+b)*(c1*y1+c2*y2+d) = 
a1*c1*x1*y1 + a1*c2*x1*y2 +
a2*c1*x2*y1 + a2*c2*x2*y2 +
a1*d*x1 + a2*d*x2 + b*c1*y1 + b*c2*y2 + b*d
")")

It is kept mostly for the sake of backward compatibility.

')#comment("decldecsript")

(* ****** ****** *)

#declnamesynop("mul_gte_gte_gte")
#decldescrpt("\
This proof function proves m >= 0 and n >= 0 implying m*n >= 0 for
any integers m and n.
")
#declnamesynop("mul_lte_gte_lte")
#decldescrpt("\
This proof function proves m <= 0 and n >= 0 implying m*n <= 0 for
any integers m and n.
")
#declnamesynop("mul_gte_lte_lte")
#decldescrpt("\
This proof function proves m >= 0 and n <= 0 implying m*n <= 0 for
any integers m and n.
")
#declnamesynop("mul_lte_lte_gte")
#decldescrpt("\
This proof function proves m <= 0 and n <= 0 implying m*n >= 0 for
any integers m and n.
")

(* ****** ****** *)

#declnamesynop("mul_nat_nat_nat")
#decldescrpt("\

This proof function establishes p >= 0 if given a proof of the type
#stacode("MUL(m, n, p)") for some natural numbers m and n. A straigtforward
implementation of #dyncode("mul_nat_nat_nat") is given as follows:

#pats2xhtmld_tryit("\
primplmnt
mul_nat_nat_nat (pf) = let
//
prfun lemma
  {m,n:nat}{p:int} .<m>.
  (pf: MUL (m, n, p)): [p >= 0] void =
  case+ pf of
  | MULind (pf1) => lemma (pf1) | MULbas () => ()
// end of [lemma]
in
  lemma (pf)
end // end of [mul_nat_nat_nat]
")
")

#declnamesynop("mul_pos_pos_pos")
#decldescrpt("\

This proof function establishes p >= m+n-1 if given a proof of
the type #stacode("MUL(m, n, p)") for some postive intgers m and n.
A direct implementation of #dyncode("mul_pos_pos_pos") based on
#dyncode("mul_nat_nat_nat") is given as follows:

#pats2xhtmld_tryit("\
primplmnt
mul_pos_pos_pos {m,n} (pf) = let
  prval MULind (pf1) = pf // pf1: MUL(m-1, n, ...)
  prval pf2 = mul_commute (pf1) // pf2: MUL (n, m-1, ...)
  prval MULind (pf3) = pf2 // pf3: MUL(m-1, n-1, ...)
in
  mul_nat_nat_nat (pf3)
end // end of [mul_pos_pos_pos]
")

")

(* ****** ****** *)

#declnamesynop("mul_negate")
#decldescrpt("\

This proof function essentially proves #stacode("(~m)*n=~(m*n)") for any
given integers m and n, where ~ is the symbol for integer negation in ATS.
Note that #dyncode("mul_negate") is kept mostly for the sake of backward
compatibility as the constraint-solver for ATS2 (Postiats) can readily
infer the identity #stacode("(~m)*n=~(m*n)"). However, typechecking the
following implementation of #dyncode("mul_negate") involves solving only
linear integer constraints:

#pats2xhtmld_tryit("\
primplmnt
mul_negate
  {m,n} (pf) = (
  sif m > 0 then let
    prval pf1 = mul_make {~m,n} ()
    prval MULneg (pf2) = pf1
    prval () = mul_isfun (pf, pf2)
  in
    pf1
  end else sif m < 0 then let
    prval MULneg (pf1) = pf in pf1
  end else let
    prval MULbas () = pf in MULbas ()
  end // end of [sif]
) // end of [mul_negate]
")
")

#declnamesynop("mul_negate2")
#decldescrpt("\

This proof function essentially proves #stacode("m*(~n)=~(m*n)") for any
given integers m and n, where ~ is the symbol for integer negation in ATS.
Note that #dyncode("mul_negate2") is kept mostly for the sake of backward
compatibility as the constraint-solver for ATS2 (Postiats) can readily
infer the identity #stacode("m*(~n)=~(m*n)"). However, typechecking the
following implementation of #dyncode("mul_negate2") involves solving only
linear integer constraints:

#pats2xhtmld_tryit("\
primplmnt
mul_negate2
  {m,n}{p} (pf) = let
//
prfun lemma
  {m:nat;n:int}
  {p:int} .<m>. (
  pf: MUL (m, n, p)
) : MUL (m, ~n, ~p) =
  case+ pf of
  | MULind
      (pf) => MULind (lemma (pf))
  | MULbas () => MULbas ()
// end of [mul_m_neg_n_neg_mn]
//
in // in of [local]
//
sif m >= 0 then
   lemma (pf)
else let
  prval MULneg pf = pf in MULneg (lemma (pf))
end // end of [sif]
//
end // end of [mul_negate2]
")
")


(* ****** ****** *)

#declnamesynop("mul_commute")
#decldescrpt("\

This proof function essentially proves #stacode("m*n=n*m") for any given
integers m and n.  It is kept mostly for the sake of backward compatibility
as the constraint-solver of ATS2 (Postiats) is able to infer the identity
#stacode("m*n=n*m") automatically.  However, typechecking the following
implementation of #dyncode("mul_commute") involves solving only linear
integer constraints:

#pats2xhtmld_tryit("\
local
//
prfun mul_m_n1_mnm
  {m,n:int}{p:int}
  .<max(2*m, 2*(~m)+1)>.
  (pf: MUL (m, n, p)): MUL (m, n+1, p+m) =
  case+ pf of
  | MULbas () => MULbas ()
  | MULind pf => MULind (mul_m_n1_mnm pf)
  | MULneg pf => MULneg (mul_m_n1_mnm pf)
// end of [mul_m_n1_mnm]
//
in // in of [local]
//
primplmnt
mul_commute
  {m,n}{p} (pf) = let
//
prfun aux
  {m:nat;n:int}
  {p:int} .<m>. (
  pf: MUL (m, n, p)
) : MUL (n, m, p) =
  case+ pf of
  | MULbas () => let
      prval pf =
        mul_istot {n,0} ()
      prval () = mul_elim pf
    in
      pf
    end // end of [MULbas]
  | MULind pf => mul_m_n1_mnm (aux pf)
// end of [aux]
//
in
  sif m >= 0 then aux pf else begin
    let prval MULneg (pf) = pf in mul_negate2 (aux pf) end
  end // end of [sif]
end // end of [mul_commute]
//
end // end of [local]
")
")

#declnamesynop("mul_is_commutative")
#decldescrpt("\

This proof function is just a slight variation of #dyncode("mul_commute"):

#pats2xhtmld_tryit("\
primplmnt
mul_is_commutative (pf1, pf2) = mul_isfun (mul_commute (pf1), pf2)
")
")

(* ****** ****** *)

#declnamesynop("mul_distribute")
#decldescrpt("\

This proof function essentially proves the identity
#stacode("m*(n1+n2)=m*n1+m*n2") for any given integers m, n1 and n2.  It is
kept mostly for the sake of backward compatibility as the constraint-solver
of ATS2 (Postiats) is able to infer the identity
#stacode("m*(n1+n2)=m*n1+m*n2") automatically.  However, typechecking the
following implementation of #dyncode("mul_distribute") involves solving only
linear integer constraints:

#pats2xhtmld_tryit("\
primplmnt
mul_distribute
  {m}{n1,n2}{p1,p2} (pf1, pf2) = let
//
prfun lemma
  {m:nat}
  {n1,n2:int}
  {p1,p2:int} .<m>. (
  pf1: MUL (m, n1, p1), pf2: MUL (m, n2, p2)
) : MUL (m, n1+n2, p1+p2) =
  case+ (pf1, pf2) of
  | (MULbas (), MULbas ()) => MULbas ()
  | (MULind pf1, MULind pf2) => MULind (lemma (pf1, pf2))
// end of [lemma]
//
in // in of [let]
//
sif m >= 0 then
  lemma (pf1, pf2)
else let
  prval MULneg pf1 = pf1 and MULneg pf2 = pf2
in
  MULneg (lemma (pf1, pf2))
end // end of [sif]
//
end // end of [mul_distribute]
")
")

#declnamesynop("mul_distribute2")
#decldescrpt("\

This proof function essentially proves the identity
#stacode("(m1+m2)*n=m1*n+m2*n") for any given integers m1, m2 and n.  It is
kept mostly for the sake of backward compatibility as the constraint-solver
of ATS2 (Postiats) is able to infer the identity
#stacode("(m1+m2)*n=m1*n+m2*n") automatically.  The following
implementation of #dyncode("mul_distribute2") is based on
#dyncode("mul_commute") and #dyncode("mul_distribute"):

#pats2xhtmld_tryit("\
primplmnt
mul_distribute2 (pf1, pf2) =
  mul_commute (mul_distribute (mul_commute (pf1), mul_commute (pf2)))
// end of [mul_distribute2]
")

")

(* ****** ****** *)

#declnamesynop("mul_is_associative")

#decldescrpt("\

This proof function essentially proves #stacode("(x*y)*z=x*(y*z)") for any
given integers x, y and z.  It is kept mostly for the sake of backward
compatibility as the constraint-solver of ATS2 (Postiats) is able to infer
the identity #stacode("(x*y)*z=x*(y*z)") automatically.  ")

(* ****** ****** *)

#declnamesynop("DIVMOD")
#decldescrpt("\

Given integers x, y, q, r, the prop #stacode("DIVMOD(x, y, q, r)")
captures that #stacode("x=q*y+r") holds and r is natural number less than
y.

")

#declnamesynop("div_istot")

#declnamesynop("divmod_istot")

#declnamesynop("divmod_isfun")

#declnamesynop("divmod_elim")

(* ****** ****** *)

#declname("EXP2")
#declsynop2("\
#para("\
The dataprop #stacode("EXP2") is declared as follows:
#pats2xhtmls("
dataprop EXP2 (int, int) =
  | {n:nat}{p:nat} EXP2ind (n+1, 2*p) of EXP2 (n, p)
  | EXP2bas (0, 1)
// end of [EXP2]
")")
")

#decldescrpt("\

Given integers n and p, the prop #stacode("EXP2(n, p)") captures the
relation that #stacode("2^n=p") holds and n is a natural number.

")

#declnamesynop("lemma_exp2_param")

#declnamesynop("exp2_istot")
#declnamesynop("exp2_isfun")
#declnamesynop("exp2_ispos")

#declnamesynop("exp2_is_mono")

#declnamesynop("exp2_muladd")

(* ****** ****** *)

#declnamesynop("EXP")
#decldescrpt("\

Given integers b, n and p, the prop #stacode("EXP(b, n, p)") captures the
relation that #stacode("b^n=p") holds and n is a natural number, where b^n
refers to the exponential function for base b and exponent n.

")

(* ****** ****** *)

#declnamesynop("lemma_exp_param")

(* ****** ****** *)

#declnamesynop("exp_istot")
#declnamesynop("exp_isfun")

(* ****** ****** *)

#declnamesynop("exp_elim_0")
#declnamesynop("exp_elim_1")
#declnamesynop("exp_elim_2")

(* ****** ****** *)

#declnamesynop("exp_elim_b_0")
#declnamesynop("exp_elim_b_1")
#declnamesynop("exp_elim_b_2")

(* ****** ****** *)

#declnamesynop("exp_muladd")
#decldescrpt("\

This proof function proves #stacode("b^(n1+n2)=(b^n1)*(b^n2)") for integer
b and natural numbers n1, n2.

")

(* ****** ****** *)

#declnamesynop("exp_expmul")
#decldescrpt("\

This proof function proves #stacode("(b^n1)^n2=b^(n1*n2)") for integer b
and natural numbers n1, n2.

")

(* ****** ****** *)

(* end of [arith_prf.atxt] *)
